/*
 * MIT License
 *
 * Copyright (c) 2017 Choko (choko@curioswitch.org)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.1'
        classpath 'com.palantir:gradle-baseline-java:0.11.1'
    }
}

plugins {
    id 'net.ltgt.apt' version '0.9' apply false
    id 'org.springframework.boot' version '1.5.1.RELEASE' apply false
}

def dependencyVersions = [
        [ 'com.google.protobuf', '3.2.0', [ 'protoc' ]],
        [ 'io.grpc', '1.1.2', [ 'grpc-core', 'grpc-protobuf', 'grpc-stub' ]],
]

apply plugin: 'com.palantir.baseline-config'
apply plugin: 'com.palantir.baseline-idea'

allprojects {
    repositories {
        jcenter()
    }

    plugins.withType(JavaPlugin) {
        project.apply plugin: 'com.palantir.baseline-idea'
        project.apply plugin: 'net.ltgt.apt'
        project.apply plugin: 'org.springframework.boot'

        group = 'org.curioswitch.curiostack'

        dependencyManagement {
            dependencies {
                dependencyVersions.each { group ->
                    dependencySet(group: group[0], version: group[1]) {
                        group[2].each {
                            entry it
                        }
                    }
                }
            }
        }
    }

    plugins.withType(JavaLibraryPlugin) {
        bootRepackage {
            enabled = false
        }
    }
}
