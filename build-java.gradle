/*
 * MIT License
 *
 * Copyright (c) 2017 Choko (choko@curioswitch.org)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

def dependencyVersions = [
        [ 'org.assertj', '3.6.2', [ 'assertj-core' ]],
        [ 'com.google.protobuf', '3.2.0', [ 'protoc' ]],
        [ 'com.google.protobuf', '0.8.1', [ 'protobuf-gradle-plugin' ]],
        [ 'io.grpc', '1.1.2', [ 'grpc-core', 'grpc-protobuf', 'grpc-stub' ]],
        [ 'io.spring.gradle', '1.0.0.RELEASE', [ 'dependency-management-plugin' ]],
        [ 'junit', '4.12', [ 'junit' ]],
]

allprojects {
    repositories {
        jcenter()
    }

    plugins.withType(JavaPlugin) {
        project.apply plugin: 'com.diffplug.gradle.spotless'
        project.apply plugin: 'com.github.hierynomus.license'
        project.apply plugin: 'com.palantir.baseline-idea'
        project.apply plugin: 'io.spring.dependency-management'
        project.apply plugin: 'net.ltgt.apt'

        group = 'org.curioswitch.curiostack'

        dependencyManagement {
            dependencies {
                dependencyVersions.each { group ->
                    dependencySet(group: group[0], version: group[1]) {
                        group[2].each {
                            entry it
                        }
                    }
                }
            }
        }

        dependencies {
            testCompile 'org.assertj:assertj-core'
            testCompile 'junit:junit'
        }

        spotless {
            java {
                googleJavaFormat()
            }
        }
    }
}
